plugins {
    id "org.openapi.generator" version "5.3.0"
}

task downloadApiSpec doLast {
    def file = new File("$projectDir/api-spec.json")
    file.delete()
    file.setText(groovy.json.JsonOutput.prettyPrint(
            new URL('http://localhost:8080/v3/api-docs').getText('UTF-8')), 'UTF-8')
}

openApiGenerate {
    generatorName = "typescript-angular"
    inputSpec = "$projectDir/api-spec.json".toString()
    outputDir = "$projectDir/src".toString()
    additionalProperties = [
            npmName       : "@andres/front-api",
            npmVersion    : "1.0.0",
            ngVersion     : "13.0.3",
            providedInRoot: "true"
    ]
    typeMappings = ["DateTime": "Date"]
}

tasks.openApiGenerate.dependsOn downloadApiSpec

task copyToFront(group: 'api', type: Sync) {
    from 'src'
    into '../front/src/gen'
    include 'api.module.ts', 'configuration.ts', 'encoder.ts', 'variables.ts', 'api/**', 'model/**'
}

copyToFront.dependsOn tasks.openApiGenerate
